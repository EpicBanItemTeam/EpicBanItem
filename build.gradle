plugins {
    id "com.github.johnrengelman.shadow" version "2.0.4"
    id "ninja.miserable.blossom" version "1.0.1"
    id "signing"
    id "java"
    id "idea"
}

def buildNumber = {
    def command = "git rev-list --count --first-parent HEAD"
    return command.execute().text.trim().toInteger() - 1
}

version = "$pluginVersion-b${buildNumber()}"

defaultTasks "clean", "build"

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        name = "sponge"
        url = "https://repo.spongepowered.org/maven"
    }
    maven {
        name = "bstats"
        url = 'http://repo.bstats.org/content/repositories/releases'
    }
}

dependencies {
    compile "org.bstats:bstats-sponge:1.2"
    compile "org.spongepowered:spongeapi:$spongeVersion"
}

jar {
    classifier = "pure"
}

shadowJar {
    classifier = null

    build.dependsOn += it

    dependencies {
        it.include it.dependency("org.bstats:.*")
    }

    relocate "org.bstats", "${project.group}.epicbanitem.util.repackage.org.bstats"
}

blossom {
    replaceToken '@version@', pluginVersion
}

signing {
    sign jar, shadowJar
    required = System.hasProperty("signing.password")
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.compilerArgs << "-Xlint:all" // << "-Werror"
}
