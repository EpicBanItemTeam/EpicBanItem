import team.ebi.gradle.TaskOreDeploy

plugins {
    id "net.kyori.blossom" version "1.3.0"
    id "java"
    id "idea"
}

def pluginVersion = {
    def command = "git describe --abbrev=0 --tags"
    return command.execute().text.trim().substring(1)
}

def buildNumber = {
    def command = "git rev-list --count --first-parent HEAD"
    return command.execute().text.trim().toInteger() - 1
}

version = "${pluginVersion()}-b${buildNumber()}"

defaultTasks "clean", "build"

sourceCompatibility = 1.8

repositories {
    maven {
        name = "sponge"
        url = "https://repo.spongepowered.org/maven"
    }
}

dependencies {
    implementation "org.spongepowered:spongeapi:$spongeVersion"
}

task oreDeploy(type: TaskOreDeploy) {
    pluginId = project.ext.id
    apiKey = System.getProperty("ore.key")
    setPluginInfo(
            recommended: true,
            createForumPost: true,
//            description: "v0.4.0",
            tags: [Channel: "PreRelease"]
    )
    jarTask = "shadowJar"
}

blossom {
    replaceToken '@version@', pluginVersion()
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.compilerArgs << "-Xlint:all" // << "-Werror"
}
